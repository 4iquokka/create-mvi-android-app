import com.shinyj.template.mvi.buildsrc.*

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion Application.compileSdk

    defaultConfig {
        applicationId Application.id
        minSdkVersion Application.minSdk
        targetSdkVersion Application.targetSdk
        versionCode Application.versionCode
        versionName Application.versionName

        testInstrumentationRunner "com.shinyj.template.mvi.MockTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    // Required for Hilt Test Implementation
    configurations.all{
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.0'
    }
}

dependencies {

    // Kotlin & Coroutines
    implementation JetBrains.Kotlin.stdlib
    implementation JetBrains.Coroutines.core
    implementation JetBrains.Coroutines.android

    implementation Google.Material.material

    implementation Android.Jetpack.coreKtx
    implementation Android.Jetpack.fragmentKtx
    implementation Android.Jetpack.appCompat
    implementation Android.Jetpack.constraintLayout
    implementation Android.Jetpack.legacy
    implementation Android.Jetpack.datastore

    // Navigation
    implementation Android.Jetpack.navigationRuntime
    implementation Android.Jetpack.navigationUi
    implementation Android.Jetpack.navigationFragment

    // Hilt
    implementation Android.Jetpack.hiltViewModel
    implementation Google.Hilt.android
    kapt Google.Hilt.compiler

    testImplementation JUnit.junit
    androidTestImplementation Android.Test.junitExt
    androidTestImplementation Android.Test.espressoCore

    //For Hilt instrumentation Test
    androidTestImplementation Google.Hilt.test
    kaptAndroidTest Google.Hilt.testCompiler

    //For Hilt local Unit Test
    testImplementation Google.Hilt.test
    kaptTest Google.Hilt.testCompiler

    androidTestImplementation Android.Test.testCoreKtx
    debugImplementation Android.Test.fragmentTest

    // Memory Leak Detection
    debugImplementation Square.LeakCanary.leakCanary
}

// Required for Hilt Implementation
kapt {
    correctErrorTypes true
}